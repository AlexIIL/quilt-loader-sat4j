buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version '4.0.4'
	id 'java'
	id 'idea'
	id 'maven-publish'
}

sourceCompatibility = 1.5
targetCompatibility = 1.5
version = '2.3.5'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

group = 'org.quiltmc'
archivesBaseName = 'quilt-loader-sat4j'

repositories {
	mavenCentral()
}

dependencies {
}

sourceSets {
	main {
		java {
			srcDir 'org.sat4j.core/src/main/java'
		}
		resources {
			srcDir 'org.sat4j.core/src/main/resources'
		}
	}
}

jar {
	classifier = "lib"
}

shadowJar {
	classifier = null
	relocate 'org.sat4j', 'org.quiltmc.loader.util.sat4j'
}

build.dependsOn(shadowJar)

publishing {
	publications {
		shadow(MavenPublication) { publication ->
			project.shadow.component(publication)
			groupId 'org.quiltmc'
			artifactId "quilt-loader-sat4j"
			version version
		}
	}
	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

artifacts {
	archives shadowJar
}
